// ------------------------------------------------------------------------------------------------
// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License. See License.txt in the project root for license information.
// ------------------------------------------------------------------------------------------------

codeunit 148089 "MTD Test Fraud Prevention Hdrs"
{
    Subtype = Test;
    TestPermissions = Disabled;

    trigger OnRun()
    begin
        // [FEATURE] [Making Tax Digital] [Fraud Prevention]
    end;

    var
        Assert: Codeunit Assert;
        LibraryMakingTaxDigital: Codeunit "Library - Making Tax Digital";
        LibraryVariableStorage: Codeunit "Library - Variable Storage";
        LibraryApplicationArea: Codeunit "Library - Application Area";
        IsInitialized: Boolean;
        NoMissingHeaderMsg: Label 'All required fraud prevention headers were provided in the latest HMRC request.';
        MissingHeadersMsg: Label 'The following fraud prevention headers were missing in the latest HMRC request:\\%1';

    [Test]
    procedure MTDMissingFraudPrevHdr_GetHeadersListString()
    var
        TempMTDMissingFraudPrevHdr: Record "MTD Missing Fraud Prev. Hdr" temporary;
    begin
        // [FEATURE] [UT]
        // [SCENARIO 349684] MTDMissingFraudPrevHdr.GetHeadersListString()
        Assert.AreEqual('', TempMTDMissingFraudPrevHdr.GetHeadersListString(), '');
        TempMTDMissingFraudPrevHdr.SafeInsert('A');
        Assert.AreEqual('A\', TempMTDMissingFraudPrevHdr.GetHeadersListString(), '');
        TempMTDMissingFraudPrevHdr.SafeInsert('B');
        Assert.AreEqual('A\B\', TempMTDMissingFraudPrevHdr.GetHeadersListString(), '');
    end;

    [Test]
    procedure MTDSessionFraudPrevHdr_GetHeadersListString()
    var
        TempMTDSessionFraudPrevHdr: Record "MTD Session Fraud Prev. Hdr" temporary;
    begin
        // [FEATURE] [UT]
        // [SCENARIO 349684] MTDSessionFraudPrevHdr.GetHeadersListString()
        Assert.AreEqual('', TempMTDSessionFraudPrevHdr.GetHeadersListString(), '');
        TempMTDSessionFraudPrevHdr.SafeInsert('A', 'a');
        Assert.AreEqual('A: a\', TempMTDSessionFraudPrevHdr.GetHeadersListString(), '');
        TempMTDSessionFraudPrevHdr.SafeInsert('B', 'b');
        Assert.AreEqual('A: a\B: b\', TempMTDSessionFraudPrevHdr.GetHeadersListString(), '');
    end;

    [Test]
    procedure DefaultHeadersAreAutoGeneratedOnOpenSetupPage()
    var
        MTDDefaultFraudPrevHdr: Record "MTD Default Fraud Prev. Hdr";
        MTDFraudPreventionHeaders: TestPage "MTD Fraud Prevention Headers";
    begin
        // [FEATURE] [UI]
        // [SCENARIO 349684] Default headers with descriptions are automatically generated on open HMRC Fraud Prevention Headers Setup page
        Initialize();
        LibraryApplicationArea.EnableFoundationSetup();

        Assert.RecordIsEmpty(MTDDefaultFraudPrevHdr);
        MTDFraudPreventionHeaders.OpenEdit();
        Assert.IsTrue(MTDFraudPreventionHeaders.Header.Visible(), '');
        Assert.IsTrue(MTDFraudPreventionHeaders.Description.Visible(), '');
        Assert.IsTrue(MTDFraudPreventionHeaders.DefaultValue.Visible(), '');
        MTDFraudPreventionHeaders.Close();
        Assert.RecordIsNotEmpty(MTDDefaultFraudPrevHdr);
    end;

    [Test]
    procedure SampleValuesAreHiddenByDefault()
    var
        MTDFraudPreventionHeaders: TestPage "MTD Fraud Prevention Headers";
    begin
        // [FEATURE] [UI]
        // [SCENARIO 349684] "Sample Value" column is hidden by default and becomes visible after "Get Current Headers" action
        Initialize();
        LibraryApplicationArea.EnableFoundationSetup();

        MTDFraudPreventionHeaders.OpenEdit();
        Assert.IsFalse(MTDFraudPreventionHeaders.SampleValue.Visible(), '');
        MTDFraudPreventionHeaders."Get Current Headers".Invoke();
        Assert.IsTrue(MTDFraudPreventionHeaders.SampleValue.Visible(), '');
        MTDFraudPreventionHeaders.Close();
    end;

    [Test]
    [HandlerFunctions('MessageHandler')]
    procedure CheckLatestHMRCRequest_Positive()
    var
        MTDFraudPreventionHeaders: TestPage "MTD Fraud Prevention Headers";
    begin
        // [FEATURE] [UI]
        // [SCENARIO 349684] "Check Latest HMR Request" action in case of a positive result (all required headers were provided)
        Initialize();

        MTDFraudPreventionHeaders.OpenEdit();
        MTDFraudPreventionHeaders."Check Latest HMRC Request".Invoke();
        MTDFraudPreventionHeaders.Close();

        Assert.ExpectedMessage(NoMissingHeaderMsg, LibraryVariableStorage.DequeueText());
        LibraryVariableStorage.AssertEmpty();
    end;

    [Test]
    [HandlerFunctions('MessageHandler')]
    procedure CheckLatestHMRCRequest_Negative()
    var
        MTDMissingFraudPrevHdr: Record "MTD Missing Fraud Prev. Hdr";
        MTDFraudPreventionHeaders: TestPage "MTD Fraud Prevention Headers";
    begin
        // [FEATURE] [UI]
        // [SCENARIO 349684] "Check Latest HMR Request" action in case of a negative result (some missing headers)
        Initialize();

        MTDMissingFraudPrevHdr.SafeInsert('A');
        MTDMissingFraudPrevHdr.SafeInsert('B');

        MTDFraudPreventionHeaders.OpenEdit();
        MTDFraudPreventionHeaders."Check Latest HMRC Request".Invoke();
        MTDFraudPreventionHeaders.Close();

        Assert.ExpectedMessage(
          StrSubstNo(MissingHeadersMsg, MTDMissingFraudPrevHdr.GetHeadersListString()),
          LibraryVariableStorage.DequeueText());
        LibraryVariableStorage.AssertEmpty();
    end;


    local procedure Initialize()
    var
        MTDMissingFraudPrevHdr: Record "MTD Missing Fraud Prev. Hdr";
        MTDDefaultFraudPrevHdr: Record "MTD Default Fraud Prev. Hdr";
        MTDSessionFraudPrevHdr: Record "MTD Session Fraud Prev. Hdr";
    begin
        MTDMissingFraudPrevHdr.DeleteAll();
        MTDDefaultFraudPrevHdr.DeleteAll();
        MTDSessionFraudPrevHdr.DeleteAll();

        if IsInitialized then
            exit;

        LibraryMakingTaxDigital.SetOAuthSetupSandbox(true);
        LibraryMakingTaxDigital.SetupOAuthAndVATRegNo(true, '', '');
        IsInitialized := true;
        Commit();
    end;

    [MessageHandler]
    procedure MessageHandler(Message: Text[1024])
    begin
        LibraryVariableStorage.Enqueue(Message);
    end;
}

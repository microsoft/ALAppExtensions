// ------------------------------------------------------------------------------------------------
// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License. See License.txt in the project root for license information.
// ------------------------------------------------------------------------------------------------
namespace Microsoft.Finance.Analysis;

using System.Telemetry;
using System.Utilities;

page 687 "Payment Practice Card"
{
    ApplicationArea = All;
    Caption = 'Payment Practice';
    PageType = Card;
    SourceTable = "Payment Practice Header";

    layout
    {
        area(Content)
        {
            group(General)
            {
                Caption = 'General';
                field("No."; Rec."No.")
                {
                    ToolTip = 'Specifies the number of the payment practice header.';
                }
                field("Aggregation Type"; Rec."Aggregation Type")
                {
                    ToolTip = 'Specifies the aggregation type of the payment practice.';
                }
                field("Header Type"; Rec."Header Type")
                {
                    ToolTip = 'Specifies the source for entries in the payment practice.';
                }
                field("Startind Date"; Rec."Starting Date")
                {
                    ToolTip = 'Specifies the starting date of the payment practice.';
                }
                field("Ending Date"; Rec."Ending Date")
                {
                    ToolTip = 'Specifies the ending date of the payment practice.';
                }
                field("Generated On"; Rec."Generated On")
                {
                    Editable = false;
                    ToolTip = 'Specifies the date and time when the payment practice was generated.';
                }
                field("Generated By"; Rec."Generated By")
                {
                    Editable = false;
                    ToolTip = 'Specifies the user who generated the payment practice.';
                }
                field("Lines Exist"; Rec."Lines Exist")
                {
                    Editable = false;
                    ToolTip = 'Specifies whether the payment practice has lines.';
                    Visible = false;
                }
                field("Modified Manually"; Rec."Modified Manually")
                {
                    Editable = false;
                    ToolTip = 'Specifies whether the payment practice was modified manually.';
                }
            }
            group("Statistics")
            {
                Caption = 'Statistics';
                field("Average Agreed Payment Period"; Rec."Average Agreed Payment Period")
                {
                    ToolTip = 'Specifies the average agreed payment period.';

                    trigger OnDrillDown()
                    begin
                        ShowHeaderDataLines();
                    end;
                }
                field("Average Actual Payment Period"; Rec."Average Actual Payment Period")
                {
                    ToolTip = 'Specifies the average actual payment period.';

                    trigger OnDrillDown()
                    begin
                        ShowHeaderDataLines();
                    end;
                }
                field("Pct Paid on Time"; Rec."Pct Paid on Time")
                {
                    ToolTip = 'Specifies the percentage paid on time.';

                    trigger OnDrillDown()
                    begin
                        ShowHeaderDataLines();
                    end;
                }
            }
            part(Lines; "Payment Practice Lines")
            {
                ApplicationArea = All;
                SubPageLink = "Header No." = field("No.");
                UpdatePropagation = Both;
                Visible = Rec."Lines Exist";
            }
        }
    }

    actions
    {
        area(Processing)
        {
            action(Generate)
            {
                Caption = 'Generate';
                ToolTip = 'Generates the data and lines for the payment practice.';
                Image = CashFlow;

                trigger OnAction()
                var
                    PaymentPractices: Codeunit "Payment Practices";
                    ConfirmManagement: Codeunit "Confirm Management";
                begin
                    if Rec."Lines Exist" then
                        if not ConfirmManagement.GetResponse(LinesWillBeDeletedQst, false) then
                            exit;

                    Rec.ClearHeader();
                    if not PaymentPractices.Generate(Rec) then
                        Message(NoEntriesFoundMsg);
                    UpdateVisibility();
                    CurrPage.Update();
                end;
            }
            action(Print)
            {
                Caption = 'Print';
                ToolTip = 'Prints the payment practice report.';
                Image = Print;

                trigger OnAction()
                begin
                    PrepareLayout(Rec."Aggregation Type");
                    Rec.SetRecFilter();
                    Report.Run(Report::"Payment Practice", false, true, Rec);
                    FeatureTelemetry.LogUptake('0000KSV', 'Payment Practices', "Feature Uptake Status"::Used);
                end;
            }
        }
        area(Promoted)
        {
            actionref(Generate_Promoted; Generate)
            {
            }
            actionref(Print_Promoted; Print)
            {
            }
        }
    }

    trigger OnOpenPage()
    begin
        CurrPage.Update();
        UpdateVisibility();
    end;

    var
        FeatureTelemetry: Codeunit "Feature Telemetry";
        LinesWillBeDeletedQst: Label 'All previously generated lines will be deleted. Do you want to continue?';
        NoEntriesFoundMsg: Label 'The payment practice generator found no entries corresponding to the header type, starting and ending date.';

    local procedure PrepareLayout(PaymentPracticeLinesAggregator: Interface PaymentPracticeLinesAggregator)
    begin
        PaymentPracticeLinesAggregator.PrepareLayout();
    end;

    local procedure ShowHeaderDataLines()
    var
        PaymentPracticeData: Record "Payment Practice Data";
    begin
        PaymentPracticeData.SetRange("Header No.", Rec."No.");
        Page.RunModal(Page::"Payment Practice Data List", PaymentPracticeData);
    end;

    local procedure UpdateVisibility()
    begin
        CurrPage.Lines.Page.UpdateVisibility(Rec."Aggregation Type", Rec."Header Type");
        CurrPage.Update();
    end;
}
